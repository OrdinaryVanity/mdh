Actor RainSpawner 15331
{
//$Category Metroid
    Radius 1
    Height 1
    +NOCLIP
    +CLIENTSIDEONLY
    +SPAWNCEILING
    +NOGRAVITY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        TNT1 A 0 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        TNT1 A 1 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        TNT1 A 0 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        TNT1 A 0 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        TNT1 A 1 A_SpawnItemEx("RainDrop", Random(-128, 128), Random(-128, 128), -2, 0, 0, 0, 0, 128, 64)
        Loop
    }
}

actor RainSound 15332
{
    +NOINTERACTION
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("ambient/rain","soundslot7")
    Spawn1:
        TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("Counter",106,"Spawn2")
        TNT1 A 0 A_GiveInventory("Counter",1)
        Loop
    Spawn2:
        TNT1 A 0 A_PlaySoundEx("ambient/rain","soundslot7")
        TNT1 A 0 A_TakeInventory("Counter",999999)
        goto Spawn1
    }
}

Actor RainDrop
{
    +Missile
    +NoBlockMap
    +NOINTERACTION
    +CLIENTSIDEONLY
    -NoGravity
    Height 2
    Radius 2
    Renderstyle Add
    Alpha 0.85
    Scale 0.25
    States
    {
    Spawn:
        RNDR A 1
        RNDR A 1 A_ChangeVelocity(0,0,-30)
        TNT1 A 0 A_CheckSight("Death")
    Spawn1:
        RNDR A 35 //A_CheckFloor("Death")//A_JumpIf(WaterLevel > 0, "Death")
        TNT1 A 0 A_CheckSight("Death")
        RNDR A 35
        goto Death
    Death:
        //RNDR BCDEFGH 3 A_FadeOut(0.15)
        TNT1 A 1
        Stop
    }
}

//////////////////////
// SNOW SPAWNERS    //
// by Tormentor667  //
// improved by Ceeb //
//////////////////////

Actor SnowParticle
{
	Radius 1
	Height 1
	Damage 0
	Projectile
	RenderStyle Translucent
	Alpha 0
	Scale 0.6

	+MISSILE 
	+NOBLOCKMAP

	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetScale(frandom(0.3, 0.6))
		SNOW AAAAAAA 2 A_FadeIn(0.1)
		SNOW A 3
		goto Spawn+9
		Death:
		SNOW A 1 A_FadeOut(0.05)
		Loop
	}
}

Actor SnowSpawner 15333
{
//$Category Metroid
	Radius 1
	Height 1

	+NOBLOCKMAP
	+NOGRAVITY
	+NOSECTOR
	+NOINTERACTION
	+NOCLIP
	-SOLID
	+CLIENTSIDEONLY
	+SPAWNCEILING

	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_CheckSight("Unsighted")
		TNT1 A 0 A_JumpIf(Args[2] > 0, "Circle")
		TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), Random(-Args[0], -Args[0]), 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), 0, 128, Args[1])
		Loop
	Circle:
		TNT1 A 8 A_SpawnItemEx ("SnowParticle", Random(-Args[0], -Args[0]), 0, 0, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, -3.0), Random(0, 360), 128, Args[1])
		Loop
	Unsighted:
		TNT1 A 1
		Goto Spawn
	}
}


ACTOR IcyStagA : Stalagmite 15334
{
//$Category Metroid
  Radius 8
  Height 66
  +SOLID
  +SPAWNCEILING
  +NOGRAVITY
  States
  {
  Spawn:
    ICT1 A -1
    Stop
  }
}

//Railbot by Captain Toenail

ACTOR RailBot 23456
{
//$Category Metroid
  Health 250
  Radius 16
  Height 16
  Speed 6
  Painchance 128
  Mass 10000000
  MaxTargetRange 384
  obituary "%o was zapped by a railbot."
  painsound "Railbot/pain"
  deathsound "Railbot/death"
  Monster
    DamageFactor "Normal", 0.1
    // Technically all this is covered by DamageFactor Normal, but I like having all the damagetypes listed in one place.
    DamageFactor "PowerBeam", 0.4
    DamageFactor "PowerCharged", 0.6
    DamageFactor "PowerCombo", 0.15
    DamageFactor "SpazerBeam", 0.1
    DamageFactor "SpazerBeamCharged", 0.3
    DamageFactor "SpazerCombo", 0.15
    DamageFactor "PlasmaBeam", 0.1
    DamageFactor "PlasmaCharged", 0.3
    DamageFactor "PlasmaCombo", 0.15
    DamageFactor "WaveBeam", 0.50
    DamageFactor "WaveCharged", 0.75
    DamageFactor "WaveCombo", 1.0
    DamageFactor "IceBeam", 0.1
    DamageFactor "IceCharged", 0.3
    DamageFactor "IceCombo", 0.15
    DamageFactor "IceBeamExplode", 0
    DamageFactor "IceChargedExplode", 0
    DamageFactor "LongBeam", 0
    DamageFactor "LongCharged", 0
    DamageFactor "LongCombo", 0
    DamageFactor "DoomCannon", 1.0
    DamageFactor "DoomCombo", 1.0
    DamageFactor "Missile", 0.45
    DamageFactor "SuperMissile", 0.65
    DamageFactor "BoostBall", 0
    DamageFactor "Bomb", 0
    DamageFactor "PowerBomb", 1
    DamageFactor "LittleMacUppercut", 0.0
    DamageFactor "SpeedBooster", 0.0
  +AMBUSH
  +SPAWNCEILING
  +NOGRAVITY
  +NOBLOOD
  +NOSPLASHALERT
  +DONTFALL
  +DONTTHRUST
  +QUICKTORETALIATE
  States
  {
  Spawn:
    AGUN A 5 A_Look
    Loop
  See:
    AGUN AAAAAAAAAA 2 A_Chase("", "Missile", CHF_DONTMOVE)
    AGUN A 0 A_PlaySound ("Railbot/Beep")
    Loop
  Missile:
    AGUN A 8 A_FaceTarget
    AGUN C 2
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 6 
    AGUN A 8 A_FaceTarget
    AGUN C 6
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 6
    AGUN A 8 A_FaceTarget
    AGUN C 6
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, -4, 0, 1)
    AGUN C 0 A_CustomMissile (RailBotBeam, -1, 4, 0, 1)
    AGUN B 2
    AGUN A 4 A_FaceTarget
    goto See
  Pain:
    AGUN A 2 A_Pain
    AGUN A 0 A_Chase
    goto See
  Death:
    AGUN A 48 A_Scream
    AGUN A 4 
    AGUN A 0 A_SpawnItemEx (RailBotBoom, 0, 0, -4, 0, 0, 0, 0, 128)
    AGUN A 0 A_PlaySound ("weapons/rocklx")
    AGUN D -1
    Stop
  }
}

Actor RailBotBeam : MageWandMissile
{
  speed 50
  damage 6
  -RIPPER
  -BLOODSPLATTER
  +SPAWNSOUNDSOURCE
  +NOEXTREMEDEATH
  +STRIFEDAMAGE
  SeeSound "Railbot/Fire"
  DeathSound "Laser/Bounce"
  Decal Bulletchip
  States
  {
  Death:
    TNT1 A 1
    TNT1 AAAAAAAAAAAAAAA 0 bright A_SpawnItemEx (BeamParticle, 0, 0, 0, random(1,4), random(1,4), random(1,-1), random(0,360), 128)
    Stop
  }
}

ACTOR BeamTrail : MageWandSmoke replaces MageWandSmoke
{
  RenderStyle Add
  Alpha 0.75
  Scale 0.025
  +NOINTERACTION
  States
  {
  Spawn:
    OPRT A 2 bright
  Fade:
    OPRT A 1 bright A_FadeOut(0.1)
    loop
  }
}

Actor BeamParticle
{
  radius 2
  height 2
  speed 0
  damage 0
  scale 0.005
  RenderStyle Add
  Alpha 0.75
  +MISSILE
  +LOWGRAVITY
  +NOBLOCKMAP
  +NOTELEPORT
  +NOCLIP
  +DONTSPLASH
  -BLOODSPLATTER
  states
  {
  Spawn:
    OPRT B 2 bright A_FadeOut(0.1)
    loop
  }
}

actor RailBotBoom
{
  Radius 2
  Height 2
  Speed 0
  Damage 0
  Scale 0.75
  renderstyle Add
  Projectile
  +NOCLIP
  States
  {
  Spawn:
  Death:
    MISL B 8 bright
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

ACTOR IceChunkSpawner
{
	Alpha 0.0
	Mass 0
	Radius 0
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAA 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,46),random(-2,2),random(-2,2),random(2,9),random(-180,180),160)
			stop
	}
}

ACTOR PowerBombActivatorActor 23457
{
//$Category Metroid - Special
  Health 1
  Radius 2
  Height 6
  DamageFactor "Normal", 0
  DamageFactor "Missile", 0
  DamageFactor "SuperMissile", 0
  DamageFactor "PowerBomb", 1
  +Shootable
  +NoGravity
  +DontThrust
  +NoBlood
  -Solid

  States
  {
	Death:
	  TNT1 A 0
	  stop
  }
}

ACTOR SuperMissileActivatorActor 23458
{
//$Category Metroid - Special
  Health 1
  Radius 2
  Height 6
  DamageFactor "Normal", 0
  DamageFactor "Missile", 1
  DamageFactor "SuperMissile", 1
  DamageFactor "PowerBomb", 0
  +Shootable
  +NoGravity
  +DontThrust
  +NoBlood
  -Solid

  States
  {
	Death:
	  TNT1 A 0
	  stop
  }
}

ACTOR BombActivatorActor 23459
{
//$Category Metroid - Special
  Health 1
  Radius 2
  Height 6
  DamageFactor "Normal", 0
  DamageFactor "Missile", 0
  DamageFactor "SuperMissile", 0
  DamageFactor "PowerBomb", 0
  DamageFactor "Bombs", 1
  DamageFactor "NoDamage", 1 //DummyExplode damagetype... WTF terminus lol
  +Shootable
  +NoGravity
  +DontThrust
  +NoBlood
  -Solid

  States
  {
	Death:
	  TNT1 A 0
	  stop
  }
}

ACTOR YllaSymbol 17858
{
//$Category Metroid
  Radius 16
  Height 16
  Gravity 0
  Health 1
  +SOLID
  +SHOOTABLE
  +DONTTHRUST
  States
  {
	Spawn:
	  TNT1 A 0
	  TNT1 A 1 ACS_Execute(888,0)
	  TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(888,0) == 1, "Spawn2")
	  TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(888,0) == 0, "Spawn3")
	  TNT1 A 1
	  YLLA A 1 BRIGHT
	  Stop
	
	Spawn2:
	  TNT1 A 0
	  TNT1 A 0 A_SetRenderStyle(1.0, STYLE_Normal)
	  YLLA A 1 BRIGHT
	  Goto Spawn
	  
	Spawn3:
	  TNT1 A 0
	  TNT1 A 0 A_SetRenderStyle(1.0, STYLE_None)
	  YLLA A 1 BRIGHT
	  Goto Spawn
	  
	Death:
	  TNT1 A 1
	  Stop
  }
}

ACTOR GrapplePoint1 : SwitchableDecoration 17859
{
//$Category Metroid
  Radius 24
  Height 48
  Gravity 0
  Health 0x7FFFFFFF
  +SOLID
  +NOGRAVITY
  //+SHOOTABLE
  +DONTTHRUST
  +SPAWNCEILING
  
  Tag "Grapple Point (Vertical)"
  
  States
  {
	Spawn:
	  TNT1 A 0
	  GPL1 A -1
	  Stop
  }
}

ACTOR GrapplePoint2 : SwitchableDecoration 17864
{
//$Category Metroid
  Radius 24
  Height 48
  Gravity 0
  Health 0x7FFFFFFF
  +SOLID
  +NOGRAVITY
  //+SHOOTABLE
  +DONTTHRUST
  
  Tag "Grapple Point (Horizontal)"
  
  States
  {
	Spawn:
	  TNT1 A 0
	  GPL2 A -1
	  Stop
  }
}